def wrapInQuotes(String wrapMe) {
    return "\"" + wrapMe + "\""
}

def gitSha1() {
    try {
        final def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return "gitless"
    }
}

def gitTag() {
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--abbrev=0', "--tags"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def findQuotedProperty(String key, String defaultValue) {
    def val = findProperty(key, defaultValue)
    if (val != defaultValue) {
        val = wrapInQuotes(val)
    }
    return val
}

def findProperty(String key, String defaultValue) {

    println "Finding '$key' property"

    // CI injected
    if (System.getenv().containsKey(key)) {
        return System.getenv(key)
    }

    // local.properties file (dev-environment)
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(propsFile.newDataInputStream())
        if (props[key] != null) {
            return props[key]
        }
    }

    // gradle properties
    def found = findProperty(key)
    if (found != null) {
        return found
    }

    // default
    return defaultValue
}

def download(String from, String to) {

    println "Dowloading from '$from' to '$to'"

    def f = new File(to)
    new URL(from).withInputStream { i ->
        f.withOutputStream {
            it << i
        }
    }

    return f
}

def downloadToProjectRoot(String from, String subfolder, String filename) {
    def to
    if (subfolder != null) {
        mkdir "${project.rootDir}/${subfolder}"
        to = "${project.rootDir}/${subfolder}/${filename}"
    } else {
        to = "${project.rootDir}/${filename}"
    }

    println "Dowloading from '$from' to '$to'"

    def f = new File(to)
    new URL(from).withInputStream { i ->
        f.withOutputStream {
            it << i
        }
    }

    return f
}


ext {
    findProperty = this.&findProperty
    findQuotedProperty = this.&findQuotedProperty
    gitSha1 = this.&gitSha1
    gitTag = this.&gitTag
    wrapInQuotes = this.&wrapInQuotes
    download = this.&download
    downloadToProjectRoot = this.&downloadToProjectRoot
}
